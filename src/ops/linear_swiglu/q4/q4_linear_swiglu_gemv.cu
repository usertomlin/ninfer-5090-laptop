#include "ops/linear_swiglu/q4/q4_linear_swiglu_kernels.h"

#include "ops/common/math.cuh"
#include "ops/common/memory.cuh"
#include "ops/common/warp.cuh"
#include "core/device.h" // CUDA_CHECK
#include "ops/linear/q4/q4_small_t_mma.cuh"

#include <cuda_bf16.h>
#include <cuda_fp16.h>

#include <cstdint>
#include <stdexcept>
#include <array>
#include <utility>

namespace ninfer::ops::detail {
namespace {

constexpr int kGroupK        = 64;
constexpr int kBytesPerGroup = 32;
constexpr int kVecBytes      = 16;
constexpr int kWarpsPerBlock = 4;
constexpr int kBlockThreads  = kWarpsPerBlock * 32;
constexpr int kPairsPerBlock = kWarpsPerBlock;
static_assert(kBytesPerGroup == 2 * kVecBytes);

template <int kIntermediate, int kK>
struct Q4SwiGluSmallTGeometry {
    static constexpr int kInputRows    = kK;
    static constexpr int kGroupsPerRow = kK / kGroupK;
};

template <int kIntermediate>
struct Q4SwiGluSmallTRows {
    static constexpr int kOutputRowsPerCta = 8;

    __device__ __forceinline__ int weight_row(int output_row0, int local_row) const {
        return output_row0 + (local_row & 7) + (local_row >= 8 ? kIntermediate : 0);
    }
};

template <int kIntermediate>
struct Q4SwiGluSmallTEpilogue {
    __nv_bfloat16* out;
    int columns;

    template <int ActiveCols>
    __device__ __forceinline__ void store(int row, int col0, float4 projected) const {
        if (col0 < columns) {
            out[static_cast<std::int64_t>(col0) * kIntermediate + row] =
                __float2bfloat16_rn(silu(projected.x) * projected.z);
        }
        if (col0 + 1 < columns) {
            out[static_cast<std::int64_t>(col0 + 1) * kIntermediate + row] =
                __float2bfloat16_rn(silu(projected.y) * projected.w);
        }
    }
};

using SmallTLauncher = void (*)(const Tensor&, const Weight&, Tensor&, cudaStream_t);

template <int kIntermediate, int kK, int ActiveCols>
void launch_small_t_active(const Tensor& x, const Weight& w, Tensor& out, cudaStream_t stream) {
    constexpr int TileCols =
        ActiveCols <= 8 ? 8 : (ActiveCols <= 16 ? 16 : (ActiveCols <= 24 ? 24 : 32));
    constexpr int kBlocks = kIntermediate / Q4SwiGluSmallTRows<kIntermediate>::kOutputRowsPerCta;
    const Q4SwiGluSmallTEpilogue<kIntermediate> epilogue{static_cast<__nv_bfloat16*>(out.data),
                                                          x.ne[1]};
    q4_small_t_mma_kernel<Q4SwiGluSmallTGeometry<kIntermediate, kK>, TileCols, ActiveCols,
                          Q4SwiGluSmallTEpilogue<kIntermediate>, Q4SwiGluSmallTRows<kIntermediate>,
                          true>
        <<<kBlocks, Q4DraftSmallTSchedule::kThreads, 0, stream>>>(
            static_cast<const __nv_bfloat16*>(x.data), static_cast<const std::uint8_t*>(w.qdata),
            static_cast<const std::uint8_t*>(w.scales), static_cast<__nv_bfloat16*>(out.data),
            epilogue, Q4SwiGluSmallTRows<kIntermediate>{}, x.ne[1]);
    CUDA_CHECK(cudaGetLastError());
}

template <int kIntermediate, int kK, std::size_t... Offsets>
constexpr auto make_small_t_launchers(std::index_sequence<Offsets...>) {
    return std::array<SmallTLauncher, sizeof...(Offsets)>{
        &launch_small_t_active<kIntermediate, kK, 2 + static_cast<int>(Offsets)>...};
}

constexpr auto kSmallTLaunchers27 = make_small_t_launchers<17408, 5120>(std::make_index_sequence<31>{});
constexpr auto kSmallTLaunchers9  = make_small_t_launchers<12288, 4096>(std::make_index_sequence<31>{});
constexpr auto kSmallTLaunchers4  = make_small_t_launchers<9216, 2560>(std::make_index_sequence<31>{});
constexpr auto kSmallTLaunchers2  = make_small_t_launchers<6144, 2048>(std::make_index_sequence<31>{});

// One warp tile covers kTileGroups 64-wide groups: 16 (32 code vectors and 2 scale vectors per
// lane set) for the 5120/4096-wide rows, 8 (16 code vectors, 1 scale vector) for the 4B's
// 2560-wide rows whose 40 groups do not divide into 16.
template <int kTileGroups>
__device__ __forceinline__ void q4_issue_pair_tile(uint4 (*__restrict__ s_code)[kTileGroups * 2],
                                                   uint4 (*__restrict__ s_scale)[2],
                                                   const std::uint8_t* __restrict__ gate_code_row,
                                                   const std::uint8_t* __restrict__ gate_scale_row,
                                                   const std::uint8_t* __restrict__ up_code_row,
                                                   const std::uint8_t* __restrict__ up_scale_row,
                                                   int tile, int lane) {
    constexpr int kVecs      = kTileGroups * 2;
    constexpr int kScaleVecs = kTileGroups / 8;
    const int g0 = tile * kTileGroups;
    if (lane < kVecs) {
        pipe_copy<16>(&s_code[0][lane],
                      reinterpret_cast<const uint4*>(gate_code_row + g0 * kBytesPerGroup) + lane);
        pipe_copy<16>(&s_code[1][lane],
                      reinterpret_cast<const uint4*>(up_code_row + g0 * kBytesPerGroup) + lane);
    }
    if (lane < kScaleVecs) {
        pipe_copy<16>(&s_scale[0][lane],
                      reinterpret_cast<const uint4*>(gate_scale_row + g0 * 2) + lane);
        pipe_copy<16>(&s_scale[1][lane],
                      reinterpret_cast<const uint4*>(up_scale_row + g0 * 2) + lane);
    }
    pipe_commit();
}

template <int kN, int kK, int kTileGroups = 16>
__global__ void q4_linear_swiglu_gemv_pair_kernel(const __nv_bfloat16* __restrict__ x,
                                                  const std::uint8_t* __restrict__ codes,
                                                  const std::uint8_t* __restrict__ scales,
                                                  __nv_bfloat16* __restrict__ out) {
    constexpr int kIntermediate    = kN / 2;
    constexpr int kGroups          = kK / kGroupK;
    constexpr int kXVecs           = kK / 8; // x as uint4 (8 bf16 each)
    constexpr int kTiles           = kGroups / kTileGroups;
    constexpr int kVecsPerWarpTile = kTileGroups * 2;
    static_assert(kIntermediate % kPairsPerBlock == 0);
    static_assert(kGroups % kTileGroups == 0);

    constexpr int kStages   = 3;
    constexpr int kPrefetch = kStages - 1;
    __shared__ __align__(16) __nv_bfloat16 x_sh[kK];
    __shared__ uint4 code_tile[kWarpsPerBlock][kStages][2][kVecsPerWarpTile];
    __shared__ uint4 scale_tile[kWarpsPerBlock][kStages][2][2];

    auto* x_sh_v    = reinterpret_cast<uint4*>(x_sh);
    const auto* x_g = reinterpret_cast<const uint4*>(x);
    for (int i = static_cast<int>(threadIdx.x); i < kXVecs; i += static_cast<int>(blockDim.x)) {
        x_sh_v[i] = x_g[i];
    }
    __syncthreads();

    const int lane    = static_cast<int>(threadIdx.x) & 31;
    const int warp    = static_cast<int>(threadIdx.x) >> 5;
    const int out_row = static_cast<int>(blockIdx.x) * kPairsPerBlock + warp;

    const std::uint8_t* gate_code_row =
        codes + static_cast<std::int64_t>(out_row) * kGroups * kBytesPerGroup;
    const std::uint8_t* gate_scale_row = scales + static_cast<std::int64_t>(out_row) * kGroups * 2;
    const std::uint8_t* up_code_row =
        codes + static_cast<std::int64_t>(out_row + kIntermediate) * kGroups * kBytesPerGroup;
    const std::uint8_t* up_scale_row =
        scales + static_cast<std::int64_t>(out_row + kIntermediate) * kGroups * 2;
    const auto* x2 = reinterpret_cast<const __nv_bfloat162*>(x_sh);

    float gate_acc = 0.0f;
    float up_acc   = 0.0f;
#pragma unroll
    for (int p = 0; p < kPrefetch; ++p) {
        if (p < kTiles) {
            q4_issue_pair_tile<kTileGroups>(code_tile[warp][p], scale_tile[warp][p], gate_code_row,
                                            gate_scale_row, up_code_row, up_scale_row, p, lane);
        } else {
            pipe_commit();
        }
    }

#pragma unroll 1
    for (int tile = 0; tile < kTiles; ++tile) {
        const int fetch = tile + kPrefetch;
        if (fetch < kTiles) {
            const int buf = fetch % kStages;
            q4_issue_pair_tile<kTileGroups>(code_tile[warp][buf], scale_tile[warp][buf],
                                            gate_code_row, gate_scale_row, up_code_row,
                                            up_scale_row, fetch, lane);
        } else {
            pipe_commit();
        }
        pipe_wait<kPrefetch>();
        __syncwarp();

        const int buf           = tile % kStages;
        const auto* gate_codes  = reinterpret_cast<const std::uint8_t*>(code_tile[warp][buf][0]);
        const auto* up_codes    = reinterpret_cast<const std::uint8_t*>(code_tile[warp][buf][1]);
        const auto* gate_scales = reinterpret_cast<const std::uint16_t*>(scale_tile[warp][buf][0]);
        const auto* up_scales   = reinterpret_cast<const std::uint16_t*>(scale_tile[warp][buf][1]);
#pragma unroll
        for (int tile_group = 0; tile_group < kTileGroups; ++tile_group) {
            const float gate_scale =
                __half2float(__ushort_as_half(static_cast<std::uint16_t>(gate_scales[tile_group])));
            const float up_scale =
                __half2float(__ushort_as_half(static_cast<std::uint16_t>(up_scales[tile_group])));

            const int gate_packed =
                static_cast<int>(gate_codes[tile_group * kBytesPerGroup + lane]);
            const int gate_q0   = sign_extend<4>(gate_packed & 0x0f);
            const int gate_q1   = sign_extend<4>(gate_packed >> 4);
            const int up_packed = static_cast<int>(up_codes[tile_group * kBytesPerGroup + lane]);
            const int up_q0     = sign_extend<4>(up_packed & 0x0f);
            const int up_q1     = sign_extend<4>(up_packed >> 4);
            const int k0        = (tile * kTileGroups + tile_group) * kGroupK + lane * 2;
            const float2 xv     = __bfloat1622float2(x2[k0 >> 1]);
            gate_acc            = fmaf(static_cast<float>(gate_q0) * gate_scale, xv.x, gate_acc);
            gate_acc            = fmaf(static_cast<float>(gate_q1) * gate_scale, xv.y, gate_acc);
            up_acc              = fmaf(static_cast<float>(up_q0) * up_scale, xv.x, up_acc);
            up_acc              = fmaf(static_cast<float>(up_q1) * up_scale, xv.y, up_acc);
        }
        __syncwarp();
    }

    gate_acc = warp_reduce_sum(gate_acc);
    up_acc   = warp_reduce_sum(up_acc);
    if (lane == 0) { out[out_row] = __float2bfloat16(silu(gate_acc) * up_acc); }
}

template <int kN, int kK, int kTileGroups = 16>
void launch_gemv(const Tensor& x, const Weight& w, Tensor& out, cudaStream_t stream) {
    constexpr int kIntermediate = kN / 2;
    const int grid = kIntermediate / kPairsPerBlock;
    q4_linear_swiglu_gemv_pair_kernel<kN, kK, kTileGroups><<<grid, kBlockThreads, 0, stream>>>(
        static_cast<const __nv_bfloat16*>(x.data), static_cast<const std::uint8_t*>(w.qdata),
        static_cast<const std::uint8_t*>(w.scales), static_cast<__nv_bfloat16*>(out.data));
    CUDA_CHECK(cudaGetLastError());
}

} // namespace

void q4_linear_swiglu_gemv_pair_launch(const Tensor& x, const Weight& w, Tensor& out,
                                       cudaStream_t stream) {
    switch (w.n) {
    case 34816:
        if (w.k != 5120 || w.padded_shape[1] != 5120) {
            throw std::invalid_argument("q4 linear_swiglu GEMV requires weight [34816,5120]");
        }
        launch_gemv<34816, 5120>(x, w, out, stream);
        return;
    case 24576:
        if (w.k != 4096 || w.padded_shape[1] != 4096) {
            throw std::invalid_argument("q4 linear_swiglu GEMV requires weight [24576,4096]");
        }
        launch_gemv<24576, 4096>(x, w, out, stream);
        return;
    case 18432:
        if (w.k != 2560 || w.padded_shape[1] != 2560) {
            throw std::invalid_argument("q4 linear_swiglu GEMV requires weight [18432,2560]");
        }
        launch_gemv<18432, 2560, 8>(x, w, out, stream);
        return;
    case 12288:
        if (w.k != 2048 || w.padded_shape[1] != 2048) {
            throw std::invalid_argument("q4 linear_swiglu GEMV requires weight [12288,2048]");
        }
        launch_gemv<12288, 2048>(x, w, out, stream);
        return;
    default:
        throw std::invalid_argument("q4 linear_swiglu GEMV: unsupported weight shape");
    }
}

void q4_linear_swiglu_small_t_tiled_launch(const Tensor& x, const Weight& w, Tensor& out,
                                           cudaStream_t stream) {
    if (x.ne[1] < 2 || x.ne[1] > 32) {
        throw std::invalid_argument("Q4 LinearSwiGLU exact small-T requires T=2..32");
    }
    const std::size_t idx = static_cast<std::size_t>(x.ne[1] - 2);
    if (w.n == 34816) {
        kSmallTLaunchers27[idx](x, w, out, stream);
        return;
    }
    if (w.n == 24576) {
        kSmallTLaunchers9[idx](x, w, out, stream);
        return;
    }
    if (w.n == 18432) {
        kSmallTLaunchers4[idx](x, w, out, stream);
        return;
    }
    if (w.n == 12288) {
        kSmallTLaunchers2[idx](x, w, out, stream);
        return;
    }
    throw std::invalid_argument("q4 linear_swiglu small_t: unsupported weight shape");
}

} // namespace ninfer::ops::detail

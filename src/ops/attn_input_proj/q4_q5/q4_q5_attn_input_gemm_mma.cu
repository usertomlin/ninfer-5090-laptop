#include "ops/attn_input_proj/q4_q5/q4_q5_attn_input_kernels.h"

#include "core/device.h"
#include "ops/common/math.h"
#include "ops/common/rowsplit_grouped_mma.cuh"
#include "ops/common/token_slices.h"

#include <cstdint>
#include <stdexcept>

namespace ninfer::ops::detail {
namespace {

RowSplitGroupedMmaJob make_job(const Weight& weight, std::int32_t row_begin, std::int32_t row_count,
                               Tensor& out) {
    if (row_begin < 0 || row_count <= 0 || row_begin + row_count > weight.n ||
        out.ne[0] != row_count) {
        throw std::invalid_argument("Q4/Q5 attention input grouped MMA row view is invalid");
    }
    const std::int64_t groups = weight.padded_shape[1] / 64;
    const auto* codes         = static_cast<const std::uint8_t*>(weight.qdata) +
                        static_cast<std::int64_t>(row_begin) * groups * 32;
    const auto* high   = weight.qtype == QType::Q5G64_F16S
                             ? static_cast<const std::uint8_t*>(weight.qhigh) +
                                 static_cast<std::int64_t>(row_begin) * groups * 8
                             : nullptr;
    const auto* scales = static_cast<const std::uint8_t*>(weight.scales) +
                         static_cast<std::int64_t>(row_begin) * groups * 2;
    return RowSplitGroupedMmaJob{
        codes,     high,      scales, static_cast<__nv_bfloat16*>(out.data),
        row_count, out.ne[0], 0,      weight.qtype == QType::Q5G64_F16S,
    };
}

template <class Schedule, RowSplitGroupedMmaCodec Codec>
void launch_pair(bool full, const Tensor& x, RowSplitGroupedMmaJob first,
                 RowSplitGroupedMmaJob second, cudaStream_t stream) {
    const int tiles = div_up(first.n, Schedule::BM) + div_up(second.n, Schedule::BM);
    const int cols  = x.ne[1];
    const dim3 grid(static_cast<unsigned>(tiles),
                    static_cast<unsigned>(div_up(cols, Schedule::BN)));
    RowSplitGroupedMmaJob empty{};

    if (full) {
        rowsplit_grouped_mma_kernel<Schedule, true, Codec, 2>
            <<<grid, Schedule::THREADS, 0, stream>>>(static_cast<const __nv_bfloat16*>(x.data),
                                                     first, second, empty, empty, x.ne[0], cols,
                                                     x.ne[0]);
    } else {
        rowsplit_grouped_mma_kernel<Schedule, false, Codec, 2>
            <<<grid, Schedule::THREADS, 0, stream>>>(static_cast<const __nv_bfloat16*>(x.data),
                                                     first, second, empty, empty, x.ne[0], cols,
                                                     x.ne[0]);
    }
    CUDA_CHECK(cudaGetLastError());
}

template <class Geometry, class Schedule>
void launch_slice(const Tensor& x, const Weight& query_key_weight, const Weight& gate_value_weight,
                  Tensor& q, Tensor& gate, Tensor& k, Tensor& v, cudaStream_t stream) {
    const bool full = (x.ne[1] % Schedule::BN) == 0;
    launch_pair<Schedule, RowSplitGroupedMmaCodec::Q4>(
        full, x, make_job(query_key_weight, 0, Geometry::kQueryRows, q),
        make_job(query_key_weight, Geometry::kQueryRows, Geometry::kKvRows, k), stream);
    launch_pair<Schedule, RowSplitGroupedMmaCodec::Q5>(
        full, x, make_job(gate_value_weight, 0, Geometry::kQueryRows, gate),
        make_job(gate_value_weight, Geometry::kQueryRows, Geometry::kKvRows, v), stream);
}

template <class Geometry, class Schedule>
void launch(const Tensor& x, const Weight& query_key_weight, const Weight& gate_value_weight,
            Tensor& q, Tensor& gate, Tensor& k, Tensor& v, cudaStream_t stream) {
    constexpr std::int32_t kSliceCols = Schedule::BN;
    for_each_token_slice(x.ne[1], kSliceCols, [&](std::int32_t offset, std::int32_t count) {
        const Tensor x_slice = x.slice(1, offset, count);
        Tensor q_slice       = q.slice(1, offset, count);
        Tensor gate_slice    = gate.slice(1, offset, count);
        Tensor k_slice       = k.slice(1, offset, count);
        Tensor v_slice       = v.slice(1, offset, count);
        launch_slice<Geometry, Schedule>(x_slice, query_key_weight, gate_value_weight, q_slice,
                                         gate_slice, k_slice, v_slice, stream);
    });
}

template <std::int32_t InputRows, std::int32_t ParentRows, std::int32_t QueryRows,
          std::int32_t KvRows>
struct AttnInputMmaGeometry {
    static constexpr std::int32_t kInputRows  = InputRows;
    static constexpr std::int32_t kParentRows = ParentRows;
    static constexpr std::int32_t kQueryRows  = QueryRows;
    static constexpr std::int32_t kKvRows     = KvRows;
};

using AttnInputMmaGeometry27 = AttnInputMmaGeometry<5120, 7168, 6144, 1024>;
using AttnInputMmaGeometry9  = AttnInputMmaGeometry<4096, 5120, 4096, 1024>;
using AttnInputMmaGeometry4  = AttnInputMmaGeometry<2560, 5120, 4096, 1024>;
// Qwen3.5-2B: 8 query heads and 2 KV heads of width 256 over hidden=2048.
using AttnInputMmaGeometry2  = AttnInputMmaGeometry<2048, 2560, 2048, 512>;
using MmaR32C64S4            = GemmCfg<32, 64, 64, 16, 16, 4, 1, false, true, true>;

template <class Fn>
void dispatch_input_geometry(std::int32_t input_rows, Fn&& fn) {
    switch (input_rows) {
    case AttnInputMmaGeometry2::kInputRows:
        return fn.template operator()<AttnInputMmaGeometry2>();
    case AttnInputMmaGeometry4::kInputRows:
        return fn.template operator()<AttnInputMmaGeometry4>();
    case AttnInputMmaGeometry9::kInputRows:
        return fn.template operator()<AttnInputMmaGeometry9>();
    case AttnInputMmaGeometry27::kInputRows:
        return fn.template operator()<AttnInputMmaGeometry27>();
    }
    throw std::invalid_argument("Q4/Q5 attention input grouped MMA: unsupported input width");
}

template <class Geometry, class S, bool Full>
void mixed_slice(const Tensor& x, const Weight& w0, const Weight& w1, Tensor& q, Tensor& g,
                 Tensor& k, Tensor& v, cudaStream_t stream) {
    const dim3 grid(2 * Geometry::kParentRows / S::BM, (x.ne[1] + S::BN - 1) / S::BN);
    rowsplit_grouped_mma_kernel<S, Full, RowSplitGroupedMmaCodec::Mixed, 4>
        <<<grid, S::THREADS, 0, stream>>>(static_cast<const __nv_bfloat16*>(x.data),
                                          make_job(w0, 0, Geometry::kQueryRows, q),
                                          make_job(w0, Geometry::kQueryRows, Geometry::kKvRows, k),
                                          make_job(w1, 0, Geometry::kQueryRows, g),
                                          make_job(w1, Geometry::kQueryRows, Geometry::kKvRows, v),
                                          Geometry::kInputRows, x.ne[1], Geometry::kInputRows);
    CUDA_CHECK(cudaGetLastError());
}

template <class Geometry, class S>
void launch_mixed(const Tensor& x, const Weight& w0, const Weight& w1, Tensor& q, Tensor& g,
                  Tensor& k, Tensor& v, cudaStream_t stream) {
    for_each_token_slice(x.ne[1], S::BN, [&](int begin, int count) {
        const Tensor xs = x.slice(1, begin, count);
        Tensor qs = q.slice(1, begin, count), gs = g.slice(1, begin, count),
               ks = k.slice(1, begin, count), vs = v.slice(1, begin, count);
        if (count % S::BN == 0)
            mixed_slice<Geometry, S, true>(xs, w0, w1, qs, gs, ks, vs, stream);
        else
            mixed_slice<Geometry, S, false>(xs, w0, w1, qs, gs, ks, vs, stream);
    });
}
} // namespace

void q4_q5_attn_input_grouped_mma_r32_c64_s4_launch(const Tensor& x, const Weight& query_key_weight,
                                                    const Weight& gate_value_weight, Tensor& q,
                                                    Tensor& gate, Tensor& k, Tensor& v,
                                                    cudaStream_t stream) {
    dispatch_input_geometry(x.ne[0], [&]<class Geometry>() {
        launch<Geometry, MmaR32C64S4>(x, query_key_weight, gate_value_weight, q, gate, k, v,
                                      stream);
    });
}

void q4_q5_attn_input_mixed_r32_c64_s3_launch(const Tensor& x, const Weight& w0, const Weight& w1,
                                              Tensor& q, Tensor& g, Tensor& k, Tensor& v,
                                              cudaStream_t stream) {
    using Schedule = GemmCfg<32, 64, 64, 16, 16, 3, 3, false, true, true>;
    dispatch_input_geometry(x.ne[0], [&]<class Geometry>() {
        launch_mixed<Geometry, Schedule>(x, w0, w1, q, g, k, v, stream);
    });
}

void q4_q5_attn_input_pair_r32_c64_s3_launch(const Tensor& x, const Weight& w0, const Weight& w1,
                                             Tensor& q, Tensor& g, Tensor& k, Tensor& v,
                                             cudaStream_t stream) {
    using Schedule = GemmCfg<32, 64, 64, 32, 16, 3, 2, false, true, true>;
    dispatch_input_geometry(x.ne[0], [&]<class Geometry>() {
        launch<Geometry, Schedule>(x, w0, w1, q, g, k, v, stream);
    });
}

void q4_q5_attn_input_mixed_r64_c128_s2_launch(const Tensor& x, const Weight& w0, const Weight& w1,
                                               Tensor& q, Tensor& g, Tensor& k, Tensor& v,
                                               cudaStream_t stream) {
    using Schedule = GemmCfg<64, 128, 64, 64, 16, 2, 2, false, true, true>;
    dispatch_input_geometry(x.ne[0], [&]<class Geometry>() {
        launch_mixed<Geometry, Schedule>(x, w0, w1, q, g, k, v, stream);
    });
}
} // namespace ninfer::ops::detail
